apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.desperado.apkreduce'
version = '0.0.1'

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "haozhou"
            name "haozhou"
            email "joe.h0430@gmail.com"
        }
    }

    scm {
        url "https://github.com/zh8637688/AndroidWebP-GradlePlugin"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.desperado.apkreduce'
            artifactId 'apkreduce'
            version '0.0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'a gradle plugin that converting image to webp for android build')
                root.appendNode('name', 'webp')
                root.appendNode('url', 'https://github.com/zh8637688/AndroidWebP-GradlePlugin')
                root.children().last() + pomConfig
            }
        }
    }
}

Properties properties = new Properties()
def file = project.rootProject.file('local.properties')
if (file.exists()) {
    properties.load(file.newDataInputStream())
}

bintray {
    user = properties.getProperty("BINTRAY_USER")
    key = properties.getProperty("BINTRAY_KEY")
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'webp'
        vcsUrl = 'https://github.com/zh8637688/AndroidWebP-GradlePlugin.git'
        labels = ['gradle', 'plugin', 'android', 'webp']
        licenses = ['Apache-2.0']
        version {
            name = '0.0.1'
            released = new Date()
        }
    }
}


uploadArchives {
    repositories {
        mavenDeployer {
            //提交到远程服务器：
            // repository(url: "http://www.xxx.com/repos") {
            //    authentication(userName: "admin", password: "admin")
            // }
            //本地的Maven地址设置为D:/repos
            repository(url: uri('./repo'))
            pom.groupId = "com.desperado.apkreduce"
            pom.artifactId = "apkreduce"
            pom.version = "0.0.1"
        }
    }
}